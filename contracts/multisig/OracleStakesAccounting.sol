/*
 * Copyright ©️ 2018 Galt•Space Society Construction and Terraforming Company
 * (Founded by [Nikolai Popeka](https://github.com/npopeka),
 * [Dima Starodubcev](https://github.com/xhipster),
 * [Valery Litvin](https://github.com/litvintech) by
 * [Basic Agreement](http://cyb.ai/QmSAWEG5u5aSsUyMNYuX2A2Eaz4kEuoYWUkVBRdmu9qmct:ipfs)).
 *
 * Copyright ©️ 2018 Galt•Core Blockchain Company
 * (Founded by [Nikolai Popeka](https://github.com/npopeka) and
 * Galt•Space Society Construction and Terraforming Company by
 * [Basic Agreement](http://cyb.ai/QmaCiXUmSrP16Gz8Jdzq6AJESY1EAANmmwha15uR3c1bsS:ipfs)).
 */

pragma solidity 0.4.24;
pragma experimental "v0.5.0";

import "openzeppelin-solidity/contracts/math/SafeMath.sol";
import "openzeppelin-solidity/contracts/token/ERC20/ERC20.sol";
import "../collections/ArraySet.sol";
import "../Oracles.sol";
import "../traits/Initializable.sol";
import "./ArbitratorVoting.sol";


contract OracleStakesAccounting is Permissionable {
  using SafeMath for uint256;
  using ArraySet for ArraySet.AddressSet;

  event OracleBalanceStake(
    address oracle,
    bytes32 oracleType,
    uint256 amount,
    int256 finalStake
  );

  event OracleBalanceSlash(
    address oracle,
    bytes32 oracleType,
    uint256 amount,
    int256 finalStake
  );

  string public constant ROLE_SLASH_MANAGER = "slash_manager";

  address slashManager;
  address multiSigWallet;
  ArbitratorVoting voting;
  ERC20 galtToken;
  Oracles oracles;
  mapping(address => OracleTypes) oracleTypes;


  struct OracleTypes {
    int256 totalStakes;
    mapping(bytes32 => int256) oracleTypeStakes;
  }

  modifier onlySlashManager {
    requireRole(msg.sender, ROLE_SLASH_MANAGER);

    _;
  }

  constructor(
    Oracles _oracles,
    ERC20 _galtToken,
    address _multiSigWallet
  )
    public
  {
    oracles = _oracles;
    galtToken = _galtToken;
    multiSigWallet = _multiSigWallet;
  }

  function setVotingAddress(ArbitratorVoting _voting) external onlyRole(ROLE_ROLE_MANAGER) {
    voting = _voting;
  }

  function slash(address _oracle, bytes32 _oracleType, uint256 _amount) external onlySlashManager {
    _slash(_oracle, _oracleType, _amount);
  }

  function slashMultiple(address[] _oracles, bytes32[] _oracleTypes, uint256[] _amounts) external onlySlashManager {
    assert(_oracles.length == _oracleTypes.length);
    assert(_oracleTypes.length == _amounts.length);

    for (uint256 i = 0; i < _oracles.length; i++) {
      _slash(_oracles[i], _oracleTypes[i], _amounts[i]);
    }
  }

  function _slash(address _oracle, bytes32 _oracleType, uint256 _amount) internal {
    require(oracles.isOracleTypeAssigned(_oracle, _oracleType), "Some oracle types doesn't match");

    int256 initialRoleBalance = oracleTypes[_oracle].oracleTypeStakes[_oracleType];
    int256 initialTotalBalance = oracleTypes[_oracle].totalStakes;
    int256 finalRoleBalance = oracleTypes[_oracle].oracleTypeStakes[_oracleType] - int256(_amount);
    int256 finalTotalBalance = oracleTypes[_oracle].totalStakes - int256(_amount);

    assert(finalTotalBalance < initialTotalBalance);
    assert(finalRoleBalance < initialRoleBalance);

    oracleTypes[_oracle].totalStakes = finalTotalBalance;
    oracleTypes[_oracle].oracleTypeStakes[_oracleType] = finalRoleBalance;

    oracles.onOracleStakeChanged(multiSigWallet, _oracle, _oracleType, finalRoleBalance);
    voting.onOracleStakeChanged(_oracle, uint256(finalTotalBalance));

    emit OracleBalanceSlash(_oracle, _oracleType, _amount, finalTotalBalance);
  }

  function stake(address _oracle, bytes32 _oracleType, uint256 _amount) external {
    oracles.requireOracleActiveWithAssignedOracleType(_oracle, _oracleType);
    galtToken.transferFrom(msg.sender, multiSigWallet, _amount);

    require(_amount > 0, "Expect positive amount");

    int256 initialTotalStakes = oracleTypes[_oracle].totalStakes;
    int256 initialRoleStake = oracleTypes[_oracle].oracleTypeStakes[_oracleType];
    int256 finalRoleStake = initialRoleStake + int256(_amount);
    int256 finalTotalStakes = initialTotalStakes + int256(_amount);

    assert(finalTotalStakes > initialTotalStakes);
    assert(finalRoleStake > initialRoleStake);

    oracleTypes[_oracle].totalStakes = finalTotalStakes;
    oracleTypes[_oracle].oracleTypeStakes[_oracleType] = finalRoleStake;

    oracles.onOracleStakeChanged(multiSigWallet, _oracle, _oracleType, finalRoleStake);
    voting.onOracleStakeChanged(_oracle, uint256(finalTotalStakes));

    emit OracleBalanceStake(_oracle, _oracleType, _amount, finalTotalStakes);
  }

  function balanceOf(address _oracle) external view returns (int256) {
    return oracleTypes[_oracle].totalStakes;
  }

  function stakeOf(address _oracle, bytes32 _oracleType) external view returns (int256) {
    return oracleTypes[_oracle].oracleTypeStakes[_oracleType];
  }
}
